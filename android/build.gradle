buildscript {
    apply from: "../gradle/versions.gradle"
    apply from: "../gradle/versions-android.gradle"

    repositories {
        mavenLocal()
        mavenCentral()
        google()
//        jcenter()
    }
    dependencies {
        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // Dokka
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        // Android
        classpath "com.android.tools.build:gradle:$android_gradle_version"
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version "$publish_plugin_version"
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            username = findProperty('OSSRH_USERNAME') ?: System.getenv('OSSRH_USERNAME') ?: ""
            password = findProperty('OSSRH_PASSWORD') ?: System.getenv('OSSRH_PASSWORD') ?: ""
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {

    repositories {
        mavenLocal()
        mavenCentral()
        google()
//        jcenter()
    }

    group = 'io.insert-koin'
    version = "$koin_android_version"

    // Dokka
    apply plugin: 'org.jetbrains.dokka'
    dokkaJavadoc {
    }
    task dokkaJar(type: Jar) {
        classifier = 'javadoc'
        from dokkaJavadoc
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = '1.8'
            apiVersion = '1.5'
            languageVersion = '1.5'
        }
    }
}